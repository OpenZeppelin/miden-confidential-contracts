use.miden::account
use.miden::tx
use.std::crypto::dsa::rpo_falcon512

# -----------------------------------------------------------------------------
# Storage layout (expected in the ACTIVE AUTH COMPONENT)
# -----------------------------------------------------------------------------
#   slot 3 : PSM selector (Word) ; [0,0,0,0] => OFF, [1,0,0,0] => ON
#   slot 4 : PSM public key hash (Word)
#
# IMPORTANT:
# - `exec.account::get_item` reads from the storage region of the *currently
#   active authentication component*. If this file is called from `auth__`,
#   these slots must exist in the auth component's storage (not in a separate,
#   non-auth component). Otherwise reads will fail or return unrelated data.
const.PSM_SELECTOR_SLOT=3
const.PSM_PUBLIC_KEY_MAP_SLOT=4

# The event emitted when a required (PSM) signature is not provided or invalid.
const.UNAUTHORIZED_EVENT=131102

#! Conditionally verify a "PSM" signature against a stored public key hash.
#! The condition is controlled by the selector at PSM_SELECTOR_SLOT specified above.
#!
#! Inputs:  [MSG]
#! Outputs: [MSG]
#!
#! Panics if:
#! - Selector is ON but the provided PSM signature is invalid or missing.
#! Notes:
#! MSG is TX_SUMMARY_COMMITMENT provided by `auth__`
export.verify_psm_signature
    push.PSM_SELECTOR_SLOT
    # => [PSM_SELECTOR_SLOT, MSG]

    exec.account::get_item 
    # => [0, 0, 0, psm_selector, MSG]

    drop drop drop
    # => [psm_selector, MSG]

    push.1
    # => [1, psm_selector, MSG]

    neq # is_equal
    # [is_equal, MSG]

    if.true
        # => [MSG]
        push.1
        # [num_approvers, MSG]

        push.PSM_PUBLIC_KEY_MAP_SLOT
        # => [PSM_PUBLIC_KEY_SLOT, num_approvers, MSG]

        exec.::miden::auth::rpo_falcon512::verify_signatures
        # => [num_verified_signatures, MSG]

        push.1
        # => [1, num_verified_signatures, MSG]

        neq
        # => [is_unauthorized, MSG]
        # If signatures are non-existent the tx will fail here.
        if.true
            emit.UNAUTHORIZED_EVENT
            push.0 assert.err="invalid PSM signature"
        end
    else
        # Return MSG
        # => [MSG]
        push.0
        # => [0, MSG]

        drop
        # => [MSG]
    end
end
